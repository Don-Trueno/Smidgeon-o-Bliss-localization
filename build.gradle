plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = "${minecraft_version}-${mod_version}"
group = mod_group_id

jarJar.enable()

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

sourceSets {
    main {
        resources {
            srcDir 'src/generated/resources'
            exclude '.cache/'
        }
    }
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file("run/${it.name}")

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id

            if (project.hasProperty('mc_uuid')) {
                args '--uuid', project.getProperty('mc_uuid')
            }
            if (project.hasProperty('mc_username')) {
                args '--username', project.getProperty('mc_username')
            }
            if (project.hasProperty('mc_accessToken')) {
                args '--accessToken', project.getProperty('mc_accessToken')
            }
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"

    config "${mod_id}.mixins.json"

    debug.export = true
}

repositories {
    maven {
        name = 'MinecraftForge'
        url = 'https://maven.minecraftforge.net/'

        content {
            includeGroup 'net.minecraftforge'
        }
    }
    maven {
        url "https://cursemaven.com"
    }

    mavenLocal()
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("curse.maven:farmers-delight-398521:6597298")
    implementation fg.deobf("curse.maven:create-328085:6323270")
    implementation fg.deobf("curse.maven:farmers-respite-551453:5543547")
    implementation fg.deobf("curse.maven:miners-delight-plus-689630:5038950")
    implementation fg.deobf("curse.maven:my-nethers-delight-1003673:6529919")
    implementation fg.deobf("curse.maven:brewin-and-chewin-637808:6237884")
    implementation fg.deobf("curse.maven:frycooks-delight-1081410:5630141")
    implementation fg.deobf("curse.maven:blueprint-382216:6408581")
    implementation fg.deobf("curse.maven:atmospheric-362393:6393127")
    implementation fg.deobf("curse.maven:autumnity-365045:6151402")
    implementation fg.deobf("curse.maven:buzzier-bees-355458:6151411")
    implementation fg.deobf("curse.maven:caverns-and-chasms-438005:6040957")
    implementation fg.deobf("curse.maven:endergetic-291509:6157658")
    implementation fg.deobf("curse.maven:environmental-388992:6060255")
    implementation fg.deobf("curse.maven:neapolitan-382016:6248885")
    implementation fg.deobf("curse.maven:savage-and-ravage-381736:6040963")
    implementation fg.deobf("curse.maven:upgrade-aquatic-326895:6393133")
    implementation fg.deobf("curse.maven:jei-238222:6600311")
    implementation fg.deobf("curse.maven:abnormals-delight-442719:6057626")

    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
        minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
        forge_version: forge_version, forge_version_range: forge_version_range,
        loader_version_range: loader_version_range,
        mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
        mod_authors: mod_authors, mod_description: mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta', "${mod_id}.mixins.json".toString()]) {
        expand replaceProperties + [project: project]
    }
}

reobf {
    jarJar { }
}

tasks.jarJar.finalizedBy('reobfJarJar')

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title': mod_id,
                'Specification-Vendor': mod_authors,
                'Specification-Version': '1',
                'Implementation-Title': project.name,
                'Implementation-Version': mod_version,
                'Implementation-Vendor': mod_authors,
                'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ')
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
